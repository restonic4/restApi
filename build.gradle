import groovy.transform.Field

plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.1-SNAPSHOT" apply false
    id 'java'
}

ext {
    mcVersion = rootProject.minecraft_version
    apiVersion = rootProject.mod_version + "-" + mcVersion
}

architectury {
    minecraft = rootProject.minecraft_version
}

sourceSets {
    main {
        java.srcDirs = ['src/main/java']
    }
}


@Field static int totalSets = 4;
def versionData = [
        "1.20.2": 3,
        "1.20": 1, "1.20.1": 1,
        "1.19.4": 2,
        "1.19.3": 4
]

static def addExclusionsForVersion(int version, Set<String> excludePatterns) {
    for (int i = 1; i <= totalSets; i++) {
        if (i != version) {
            for (String category : Arrays.asList(
                    "BlockRegistry", "CreativeTabRegistry", "SoundRegistry",
                    "ItemRegistry", "CustomBlockProperties", "CustomItemProperties",
                    "CustomToolTier", "OreGenerator", "BiomeConditions",
                    "CustomBiomeTags", "CustomArmorMaterial", "RestItem")) {
                excludePatterns.add(String.format("**/%sSet%d.java", category, i));
            }
        }
    }
}

subprojects {
    apply plugin: "dev.architectury.loom"
    apply plugin: "java"

    sourceSets {
        main {
            java.srcDirs = ['src/main/java']

            java {
                include '**/*.java'

                Set<String> excludePatterns = new HashSet<>()

                def versionNumber = versionData.find { mcVersion.contains(it.key) }?.value

                if (versionNumber) {
                    addExclusionsForVersion(versionNumber, excludePatterns)
                    excludePatterns.forEach(exclusion -> exclude(exclusion))
                }
            }
        }
    }

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings loom.officialMojangMappings()
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    archivesBaseName = rootProject.archives_base_name
    version = rootProject.mod_version + "-" + rootProject.minecraft_version
    group = rootProject.maven_group

    repositories { }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 17
    }

    java {
        withSourcesJar()
    }
}